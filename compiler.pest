char = _{ ASCII_ALPHA }
digit = _{ ASCII_DIGIT }

word = @{ char+ ~ ("_" ~ char+)* }
number = @{ "-"? ~ digit+ }

label = ${ label_interior ~ ":" }
label_interior = @{ word | digit+ }

opcode = @{ char{1,4} }
register = @{ "r" ~ 'A'..'D' }
operand = { register | number | label_interior }

blank_line = { WHITESPACE* ~ COMMENT? ~ NEWLINE }
line = { label? ~ opcode ~ operand* ~ COMMENT? ~ NEWLINE }

WHITESPACE = _{ " " | "\t" }
COMMENT = _{ "#"+ ~ (!"\n" ~ ANY)* }

file = {
    SOI ~
    (line | blank_line)* ~
    EOI
}